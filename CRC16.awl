FUNCTION "CRC16" : VOID

VAR_INPUT
  DB_NO : INT ;
  DBB_Start : INT ;
  DBB_Counts : INT ;
END_VAR
VAR_OUTPUT
  CRC_H : BYTE ;
  CRC_L : BYTE ;
END_VAR
VAR_TEMP
  CRC_REG : WORD ;
  DB_NO_TEMP : INT ;
  BytesChecked : INT ;
  BitsChecked : INT ;
END_VAR

BEGIN

NETWORK
TITLE =

      L     W#16#FFFF;
      T     #CRC_REG; // Initialize CRC register
      L     #DB_NO;
      T     #DB_NO_TEMP;
      OPN   DB [#DB_NO_TEMP];
      L     #DBB_Start; // Load start byte for checksum
      SLD   3;
      LAR1  ;

      L     #DBB_Counts; // Load number of bytes for checksum
_A:   T     #BytesChecked; // Byte processing loop
      L     DBB [AR1,P#0.0];
      L     LB     1;
      XOW   ;
      T     LB     1;

      L     8;
_B:   T     #BitsChecked; // Bit processing loop
      L     #CRC_REG;
      SRW   1;
      T     #CRC_REG;
      JZ    _C; // Check if shifted out bit is 1
      L     #CRC_REG;
      L     W#16#A001;
      XOW   ;
      T     #CRC_REG;

_C:   L     #BitsChecked; // Check next bit
      LOOP  _B;

      +AR1  P#1.0; // Check next byte
      L     #BytesChecked;
      LOOP  _A;

      // Output checksum
      L     LB     1;
      T     #CRC_H;
      L     LB     0;
      T     #CRC_L;
END_FUNCTION
